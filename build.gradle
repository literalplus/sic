plugins {
    id "org.springframework.boot" version "2.6.1"
    id "com.github.node-gradle.node" version "3.1.1"
}

group 'plus.lit'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'io.spring.dependency-management'

sourceCompatibility = 17
targetCompatibility = 17
ext.genResDir = "$buildDir/resources/generated"

repositories {
    mavenCentral()
    maven { url 'https://repo.grails.org/grails/plugins-releases-local' }
}

dependencies {
    implementation(group: 'org.springframework.boot', name: 'spring-boot-starter-web') {
        exclude module: 'spring-boot-starter-tomcat'
    }
    implementation(group: 'org.springframework.boot', name: 'spring-boot-starter-jetty') {
        exclude group: 'org.apache.tomcat.embed', module: 'tomcat-embed-el'
    }
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-security'
    implementation(group: 'org.springframework.boot', name: 'spring-boot-starter-validation') {
        exclude module: 'tomcat-embed-el'
    }
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-thymeleaf'

    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
    compileOnly group: 'org.springframework.boot', name: 'spring-boot-devtools'

    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'
    implementation group: 'io.jsonwebtoken', name: 'jjwt', version: '0.6.0'

    implementation group: 'org.postgresql', name: 'postgresql'
    implementation group: 'org.flywaydb', name: 'flyway-core', version: '8.2.1'

    annotationProcessor group: 'org.springframework.boot', name: 'spring-boot-configuration-processor'

    implementation group: 'com.timgroup', name: 'java-statsd-client', version: '3.1.0'
    implementation group: 'io.dropwizard.metrics', name: 'metrics-core'

    implementation group: 'org.webjars', name: 'jquery', version: '2.1.4'
    implementation group: 'org.webjars', name: 'angular-ui-bootstrap', version: '1.1.1-1'
    implementation group: 'org.webjars', name: 'angular-ui-router', version: '0.2.17'
    implementation group: 'org.webjars', name: 'bootswatch-paper', version: '3.3.5+4'
    implementation group: 'org.webjars.npm', name: 'lodash', version: '4.11.1'
    implementation group: 'org.webjars', name: 'ngInfiniteScroll', version: '1.2.1'
}

compileJava.dependsOn(processResources)

bootJar {
    archiveFileName = 'sic.jar'
}

bootRun {
    sourceResources sourceSets.main
}

node {
    download = true
    version = "12.18.3"
    npmInstallCommand = "ci"
}

npm_run_less {
    dependsOn npmInstall
    args = [
            "src/main/less/sic.less",
            "${project.genResDir}/static/css/sic.css",
    ]
    inputs.files('package.json', 'package-lock.json')
    inputs.dir('src/main/less')
    inputs.dir(fileTree("node_modules").exclude(".cache"))
    outputs.dir("${project.genResDir}/static/css")
}

npm_run_combinejs {
    dependsOn npmInstall
    inputs.files('package.json', 'package-lock.json')
    inputs.dir('src/main/js')
    inputs.dir(fileTree("node_modules").exclude(".cache"))
    outputs.dir("${project.genResDir}/static/js")
}

"npm_run_copy-fonts" {
    dependsOn npmInstall
    inputs.dir(fileTree("node_modules").exclude(".cache"))
    outputs.dir("${project.genResDir}/static/fonts")
}

sourceSets.main.resources {
    srcDirs = [ "src/main/resources", project.genResDir]
}

processResources.dependsOn 'npm_run_less'
processResources.dependsOn 'npm_run_combinejs'
processResources.dependsOn 'npm_run_copy-fonts'

